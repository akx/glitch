define "contextfx", ["./utils", "./blend", "./StackBlur"],
	!(
		{zclamp},
		{dataBlend},
		{stackBlurImageData}
	)->

		bloom = !(context, radius, strength, counter) ->
			return if strength <= 0 or radius <= 0
			{width, height} = context.canvas
			data = context.getImageData 0, 0, width, height
			blurData = context.getImageData 0, 0, width, height
			stackBlurImageData blurData, 0, 0, width, height, radius
			data-blend blurData, data, strength, counter, "screen"
			context.putImageData data, 0, 0



		afterImage = !(context, data, strengthOut, counterOut, strengthIn) ->
			if context.afterimageData then
				data-blend context.afterimageData, data, strengthOut, counterOut, "screen"

			if context.afterimageData and strengthIn < 1 then
				data-blend data, context.afterimageData, strengthIn, (1.0 - strengthIn), "normal"
			else
				context.putImageData(data, 0, 0)
				context.afterimageData = context.getImageData(0, 0, data.width, data.height)


		return {
			bloom
			afterImage
		}